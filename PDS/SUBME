/* REXX
 |
 | Name: SUBME
 |
 | Author: David Alcock :: dave@planetmvs.com
 |
 | Purpose: Edit macro that submits the current dataset/member to the
 |          Job Entry subsystem.  Use this when ISPF's submit fails
 |          due to ISPF work dataset space problem.
 |
 | Disclaimer: This REXX exec is FREEWARE.  Use at your own risk.  It
 |             is provided for your enjoyment and neither David
 |             Alcock or his employer provides any warranty for it's
 |             use.  I'd like to hear how it works on your system.
 |
 |             This software is not in the public domain but is
 |             available free of charge and with source code
 |             provided.  It is copyright 1991-2002+ by David Alcock
 |             All rights reserved.
 */
address ISREDIT "MACRO (PARMS) NOPROCESS"
if rc <> 0 then do
   ZEDSMSG = "Not an EDIT macro"
   ZEDLMSG = "This command can only be issued as an ISPF EDIT macro"
   address ISPEXEC "SETMSG MSG(ISRZ001)"
   exit 12
   end
parms = translate(parms)
address ISREDIT "(THEDSN) = DATASET"
address ISREDIT "(MEMBER) = MEMBER"
if member <> '' then thedsn = thedsn||"("||member||")"

address TSO "SUBMIT '"thedsn"'"
subrc = rc
if subrc == 0 then do
   SDate = Date()                    /* Get submit Date             */
   if length(SDate) == 10 ,          /* Short Date: "4 Jul 1990"?   */
      then SDate = "0"SDate          /* ..Yes make it "04 Jul 1990" */
   SDate = Overlay('-',SDate,3)      /* Insert "04-Jul 1990"        */
   SDate = Overlay('-',SDate,7)      /* Insert "04-Jul-1990"        */
   say "          at "TIME() "on" SDate
   say "..from dsn='"thedsn"'"
   end
else do
     say "%SUBME - Submit failed, rc="subrc" +"
     say "       > dsn='"thedsn"'"
     end
