/* Rexx
 |
 | Name: OSCMD
 |
 | Author: David Alcock :: dave@planetmvs.com
 |
 | Purpose: Issue operator command and show on TSO screen
 |
 | Restrictions: You must have security system (RACF ACF2, etc)
 |               access to the CONSOLE command.
 |
 | Disclaimer: This REXX exec is FREEWARE.  Use at your own risk.  It
 |             is provided for your enjoyment and neither David
 |             Alcock or his employer provides any warranty for it's
 |             use.  I'd like to hear how it works on your system.
 |
 |             This software is not in the public domain but is
 |             available free of charge and with source code
 |             provided.  It is copyright 2001-2002+ by David Alcock
 |             All rights reserved.
 */

arg cmd
if cmd == "" then cmd = "D IPLINFO"
name = sysvar('sysuid')"O"

display = 1
if substr(cmd,1,7) == "NODISP "   then do
   cmd = delstr(cmd,1,7)
   display = 0
   end

ADDRESS TSO
"CONSPROF SOLDISP(NO) UNSOLDISP(NO) SOLNUM(400)"
crc = rc
if crc <> 0 then exit
"CONSOLE ACTIVATE NAME("name")"
crc = rc
if crc <> 0 then exit

ADDRESS CONSOLE cmd
GETCODE = GETMSG('CONMSG.','SOL',,,100)

ADDRESS TSO
say "RC="getcode "for" cmd

if display == 1 then ,          /* Echo output */
   do i = 1 to conmsg.0
      say conmsg.i
      end

"CONSOLE DEACTIVATE"
EXIT
