/* REXX
 |
 | Name: APFADD
 |
 | Author: David Alcock :: dave@planetmvs.com
 |
 | Purpose: Add a dataset to the APFLST using the OSCMD Rexx exec;
 |
 | Hint: Can be used from ISPF 3.4
 |
 | Syntax: APFADD 'SYS2.BOZO.CLOWNLIB'
 |
 | Requirements: Rexx exec OSCMD (file 311 from http://www.cbttape.org)
 |               or equivalent;
 |
 | Disclaimer: This REXX exec is FREEWARE.  Use at your own risk.  It
 |             is provided for your enjoyment and neither David
 |             Alcock or his employer provides any warranty for it's
 |             use.  I'd like to hear how it works on your system.
 |
 |             This software is not in the public domain but is
 |             available free of charge and with source code
 |             provided.  It is copyright 1997-2002+ by David Alcock
 |             All rights reserved.
 |
 */

/*---------------------------------------------------------------------
 | Installation dependant
 ------------------------------------------------------------------- */

mcs_cmd = "%OSCMD  "   /* Installation dependant */

/*---------------------------------------------------------------------
 | Verify the dataset
 ------------------------------------------------------------------- */

arg dsn vol nodelay

if dsn == "" then do
   say "Missing dataset name"
   exit
   end

dodelay = 1
if translate(vol) == "NODELAY" then do
   dodelay = 0
   vol = ""
   end
if translate(nodelay) == "NODELAY" then dodelay = 0

if vol == "" then do
   say "Missing volume serial, will use cataloged version"
   x = LISTDSI(dsn)
   if x <> 0 then do
      say "Error: Dataset not found:" dsn
      exit
      end
   vol = sysvolume
   say "Using volume "vol" for dsn="dsn
   end

vol = strip(vol)
x = LISTDSI(dsn "VOLUME("vol")" 'NORECALL' )
if x <> 0 then do
   say "LISTDSI failed to DSN="dsn "VOL="vol
   exit
   end

if sysrecfm <> "U" then do
   say "Dataset does not appear to be a load library - RECFM="sysrecfm
   say "It was NOT added to the APFLST"
   exit
   end
if sysdsorg <> "PO" then do
   say "Dataset does not appear to be a load library - DSORG="sysdsorg
   say "It was NOT added to the APFLST"
   exit
   end

/*---------------------------------------------------------------------
 | Issue the operator command
 ------------------------------------------------------------------- */

say "Issuing command: SETPROG APF,ADD,DSN="sysdsname",VOL="sysvolume
address TSO mcs_cmd  "SETPROG APF,ADD,DSN="sysdsname",VOL="sysvolume
xtime = time()

/* When you issue a "D PROG,APF" right after an add, the new dataset
   doesn't show up in the list - A delay or wait command would be
   better than this do loop */
if dodelay then do
   do i = 1 to 100000  /* put in a short delay */
      nop
      end
   address TSO mcs_cmd "NODISP D PROG,APF"
   say "Look in the SYSLOG around "time()" for D PROG,APF"
   say "New datasets are at the end of the D PROG,APF list"
   end

exit
